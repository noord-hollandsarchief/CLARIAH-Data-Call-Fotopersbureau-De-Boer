PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT 
    ?concept
    ?prefLabelNL
    ?prefLabelEN
    (GROUP_CONCAT(DISTINCT ?altLabelNL; SEPARATOR="; ") AS ?altLabelsNL)
    (GROUP_CONCAT(DISTINCT ?altLabelEN; SEPARATOR="; ") AS ?altLabelsEN)
    ?concept_broader
    ?concept_broaderLabelNL
    ?concept_broaderLabelEN
    (GROUP_CONCAT(DISTINCT ?concept_closeMatch; SEPARATOR="; ") AS ?concept_closeMatches)
    (GROUP_CONCAT(DISTINCT ?definitionNL; SEPARATOR="; ") AS ?definitionsNL)
    (GROUP_CONCAT(DISTINCT ?definitionEN; SEPARATOR="; ") AS ?definitionsEN)
WHERE {  
  ?concept a skos:Concept ;
      skos:inScheme <https://digitaalerfgoed.poolparty.biz/nha/7325bb27-69b0-4c36-9f0c-914ba5d49faf> .  
  
  # Position of the concept in hierarchy (broader/narrower)
  OPTIONAL {
    ?concept skos:broader ?concept_broader .
    ?concept_broader skos:prefLabel ?concept_broaderLabelNL .
    FILTER(LANG(?concept_broaderLabelNL) = 'nl')
  }
  
  OPTIONAL {
    ?concept skos:broader ?concept_broader .
    ?concept_broader skos:prefLabel ?concept_broaderLabelEN .
    FILTER(LANG(?concept_broaderLabelEN) = 'en')
  }
  
  # Preferred and alternative labels
  OPTIONAL { 
    ?concept skos:prefLabel ?prefLabelNL .
    FILTER(LANG(?prefLabelNL) = 'nl')
  }
  
  OPTIONAL { 
    ?concept skos:prefLabel ?prefLabelEN .
    FILTER(LANG(?prefLabelEN) = 'en')
  }
  
  OPTIONAL { 
    ?concept skos:altLabel ?altLabelNL .
    FILTER(LANG(?altLabelNL) = 'nl')
  }
  
  OPTIONAL { 
    ?concept skos:altLabel ?altLabelEN .
    FILTER(LANG(?altLabelEN) = 'en')
  }
  
  # Any external link, such as AAT or GTAA
  OPTIONAL {
    ?concept skos:closeMatch ?concept_closeMatch .
  }
  
  # Definition
  OPTIONAL { 
    ?concept skos:definition ?definitionNL .
    FILTER(LANG(?definitionNL) = 'nl')
  }

  OPTIONAL { 
    ?concept skos:definition ?definitionEN .
    FILTER(LANG(?definitionEN) = 'en')
  }
      
} GROUP BY ?concept ?prefLabelNL ?prefLabelEN ?concept_broader ?concept_broaderLabelNL ?concept_broaderLabelEN 
ORDER BY ?concept_broaderLabelNL ?concept_broaderLabelEN